var z[ nAllRows ];

model{

  ############################
  ##### Recapture model#######
  ############################

  for(i in 1:nEvalRows){

    logit( p[ evalRows[i] + 1 ] ) <- pBetaInt[ species[ evalRows[i] + 1 ],season[ evalRows[i] + 1 ],riverDATA[ evalRows[i] + 1 ],year[ evalRows[i] + 1 ] ]
#                                   +    pBeta[ species[ evalRows[i] + 1 ],riverDATA[ evalRows[i] + 1 ] ] * ( nPasses[ evalRows[ i ] + 1 ] - 1 )

  }
  ############## Recapture priors ##################
  for( spp in 1:nSpecies ){
    for( r in 1:(nRivers) ){
      for( s in 1:nSeasons ){
        for(y in 1:nYears){

        #  pBetaInt[ spp,s,r,y ] ~ dnorm( 0,0.667 )
          pBetaInt[ spp,s,r,y ] ~ dnorm( pBetaIntMean[ spp,s ], pBetaIntTau[ spp,s ] )
          phiBetaInt[ spp,s,r,y ] ~ dnorm( phiBetaIntMean[ spp,s ], phiBetaIntTau[ spp,s ] )

        }
      }
    }
  }

  for( s in 1:nSeasons ){
    for( spp in 1:nSpecies ){
      pBetaIntMean[ spp,s ] ~ dnorm( 0,1.5 )T(-3.5,3.5)
      pBetaIntSigma[ spp,s ] ~ dunif(0,10)
      pBetaIntTau[ spp,s ] <- 1/(pow(pBetaIntSigma[ spp,s ],2))

      phiBetaIntMean[ spp,s ] ~ dnorm( 0,1.5 )T(-3.5,3.5)
      phiBetaIntSigma[ spp,s ] ~ dunif(0,10)
      phiBetaIntTau[ spp,s ] <- 1/(pow(phiBetaIntSigma[ spp,s ],2))
    }
  }


  ############################
  ##### Survival model########
  ############################

  for(i in 1:nEvalRows){

    logit( phi[ evalRows[i] ] ) <-
      phiBetaInt[ species[ evalRows[i] ],season[ evalRows[i] ],riverDATA[ evalRows[i] ],year[ evalRows[i] ] ]

  }

  ############################
  ##### Likelihoods ##########
  ############################

  # Initial conditions:
  # 1) individuals enter the sample with probability 1
  # 2) individuals enter the sample alive, with probability 1

  for(i in 1:nFirstObsRows){
    z[ firstObsRows[i] ] <- 1
  }

  for(i in 1:nEvalRows){

    # State of alive (z)
    z[ evalRows[i] + 1 ] ~ dbern( survProb[ evalRows[i] ] ) #Do or don't suvive to i + 1
    survProb[evalRows[i]] <- phi[ evalRows[i] ] #^ ( sampleInterval[ evalRows[i] ] )
                             * z[ evalRows[i] ]

    # Observation of live encounters
    encDATA[ evalRows[i] + 1 ] ~ dbern( obsProb[ evalRows[i] + 1 ] )

    obsProb[ evalRows[i] + 1 ] <-
      p[ evalRows[i] + 1 ]                 # Capture probability (calculated above).
    * z[ evalRows[i] + 1 ]                 # Must be alive to be capturable.
 #   * availableDATA[ evalRows[i] + 1 ]     # Must be on the study site to be capturable. Not needed - censored in createCmrData()
#    * propSampledDATA[ season[ evalRows[i] + 1 ],riverDATA[ evalRows[i] + 1 ], year[ evalRows[i] + 1 ] ]   # proportion of the stream section that was sampled for each season,river,year combination
  }

} #model bracket
